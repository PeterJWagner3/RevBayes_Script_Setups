clear();
setwd("/Users/peterjwagner/Documents/RevBayes_Projects/");	#CHANGE THIS TO THE FOLDER IN WHICH YOU HAVE REVBAYES SCRIPTS & DATA!!!
source("scripts/Imperio_Default_Settings.Rev");

###############################################################################
# This is (these are) the nexus file(s) that you are using for this analysis  #
#     Make sure that filenames & directories are correct!!!
###############################################################################
filenames <- v("data/Vascoceratidae_Matrix_2_States.nex", "data/Vascoceratidae_Matrix_3_States.nex", "data/Vascoceratidae_Matrix_4_States.nex", "data/Vascoceratidae_Matrix_5_States.nex", "data/Vascoceratidae_Matrix_6_States.nex", "data/Vascoceratidae_Matrix_7_States.nex", "data/Vascoceratidae_Matrix_9_States.nex");

partition_states <- v(2,3,4,5,6,7,9);
partition_ordering <- v("unordered","unordered","unordered","unordered","unordered","unordered","unordered");
coding_bias <- v("all","all","all","variable","variable","variable","variable");	## prepare for ascertainment bias in binary characters; 'all': invariant & autapomorphies present; 'variable': all vary & autapomorphies present; 'informative': all vary & no autapomorphies.
max_age <- 9.1;						# used if big_bang==TRUE;

outgroup = clade("Acanthoceras");
ingroup = clade("Dunveganoceras","Calycoceras","Spathites","Mammites","Vascoceras","Fagesia","Neoptychites","Cibolaites","Infabricaticeras","Hoplitoides","Coilopoceras");
among_char_var <- "ENTER_THE_AMONG-CHARACTER_RATE_DISTRIBUTION_YOU_WISH_TO_USE_HERE";	# enter "gamma" or "lognormal"
clock_model <- "ENTER_THE_CLOCK_MODEL_YOU_WISH_TO_USE_HERE";	# enter "strict" for strict clock, or "lognormal" for relaxed clock with lognormal variation; we'll add "dirichlet eventually;

############################################################################
#          Set a variable for the number of character partitions           #
############################################################################
n_data_subsets <- filenames.size();
taxa <- readTaxonData(file="data/vascoceratidae_fossil_intervals_FA.tsv");
n_taxa <- taxa.size();
n_branches <- (2 * n_taxa) - 2;
moves = VectorMoves();

############################################################################
# Set up appropriate parameters for speciation, extinction & sampling.     #
#      We also set up the tree search here.                                #
#                                                                          #
# NOTE: This will sometimes freeze; if it does, then edit the script so    #
#      origination & extinction are set to 1.0. This usually works!        #
############################################################################
source("scripts/Accio_Vascoceratidae_Range_Based_FBD_Parameterization.Rev");

############################################################################
# Set up appropriate Q-matrices for the partitions
#   as well as the among-character and among-branch
#   rate variation models
#  (Again, make sure that the directory is OK)
############################################################################
source("scripts/Accio_Parameters_for_Analysis_Partitioned_by_States_and_Ordering_and_Class.Rev");

############################################################################
# Wrap it all into your model
############################################################################
mymodel = model(tau,Q);		# tau should have FBD & character evolution models attached to it

############################################################################
# Add monitors & commence MCMC'ing
#  (Again, make sure that the source directory is OK)
# NOTE: the program saves trees once every printgen generations; so, the
#   lower the number, the more trees you save.
############################################################################
monitors = VectorMonitors();
## alter the file names to better reflect the analysis (e.g., vascoceratidae_strict_clock.log and vascoceratidae_strict_clock.trees)
monitors.append(mnModel(filename="output/vascoceratidae.log", printgen=10));
monitors.append(mnFile(tau, filename="output/vascoceratidae.trees",printgen=10,separator=TAB,tau));
monitors.append(mnScreen(printgen=500, speciation_rate, extinction_rate, psi, summed_gaps, num_samp_anc, origin_time));

    ################################################################################
    # Here are some starting parameters for your MCMC analysis: but use your own!
    # NOTE: as the number of moves increases, the greater the number of generations
    #     we need to make a thorough search of parameter space.  So, as taxa and
    #     and complexity of character evolution models increases, the greater the
    #     number of generations you should use.
    ################################################################################
no_runs=3;		# Number of independent MCMC analyses. (Even MCMC can get stuck in local optima!)
burnin_gens=10000;	# Number of generations for the burnin pre-analysis (to tune parameters).
tuning_int=200;		# Frequency at which burnin analysis will tune parameters (in generations).
running_gens=1000000;	# Number of generations for the real analysis; the bigger the analysis, the more you usually need.

# Now, go read Anna Karenina.....
source("scripts/Expecto_MCMC_with_Partitioned_Characters.Rev");
# .......
# Sigh: nobody remembers elementary train safety anymore.  Oh, your trees are done.

############################################################################
# Prepare MCMC output to get consensus tree(s) and the most probable tree(s)
#    As always, double check the directories....
############################################################################
tree_files <- v("output/vascoceratidae_run_1.trees","output/vascoceratidae_run_2.trees","output/vascoceratidae_run_3.trees");
maj_rule_files <- v("output/vascoceratidae_run_1_maj_rule.tre","output/vascoceratidae_run_2_maj_rule.tre","output/vascoceratidae_run_3_maj_rule.tre");
most_probable_files <- v("output/vascoceratidae_run_1_simple_map.tre","output/vascoceratidae_run_2_simple_map.tre","output/vascoceratidae_run_3_simple_map.tre");

source("scripts/Accio_Consensus_Tree.Rev");

source("scripts/Accio_Most_Probable_Tree.Rev");
